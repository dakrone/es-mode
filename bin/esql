#!/usr/bin/env bb

;; Attempt to use a dumb terminal
(System/setProperty "TERM" "dumb-color")

(require '[babashka.cli :as cli]
         '[babashka.http-client :as http]
         '[clojure.java.io :as io]
         '[cheshire.core :as json])
(import '[org.jline.terminal TerminalBuilder]
        '[org.jline.reader LineReaderBuilder])

(def cli-options {:U {:default "elastic"}
                  :P {:default "password"}
                  :S {:default "http://localhost:9200"}})

(def *url* (atom "http://localhost:9200"))

(def *user* (atom "elastic"))

(def *pass* (atom "password"))

(def PROMPT "esql> ")

(defn get-url [url body]
  (http/post url {:basic-auth [@*user* @*pass*]
                  :headers {:content-type "application/json"}
                  :query-params {:format "txt"
                                 :allow_partial_results true
                                 :pretty true}
                  :throw false
                  :body (json/encode {:query body})}))

(defn invoke-cmd [cmd]
  (println "Issuing request for" (str "[" cmd "]..."))
  (println (:body (get-url (str @*url* "/_query") cmd))))

(defn repl []
  (let [terminal (-> (TerminalBuilder/builder) (.dumb true) (.build))
        reader   (-> (LineReaderBuilder/builder)
                   (.terminal terminal)
                   (.build))]
   (try
     (loop []
       (when-let [line (.readLine reader PROMPT)]
         (when-not (clojure.string/blank? line)
           (try
             (invoke-cmd line)
             (catch java.net.ConnectException e
               (println "Unable to connect to" @*url*))))
         (recur)))
     (catch org.jline.reader.EndOfFileException _
       (println "Goodbye!"))
     (finally
       (.close terminal)))))

(let [cli-opts (cli/parse-opts *command-line-args* {:spec cli-options})
      user (:U cli-opts)
      pass (:P cli-opts)
      url (:S cli-opts)]
  (reset! *url* url)
  (reset! *user* user)
  (reset! *pass* pass)
  (println (str "Connecting to [" @*url* "] as user [" @*user* "]..."))
  (repl))
